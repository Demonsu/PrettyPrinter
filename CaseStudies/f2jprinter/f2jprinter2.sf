type Thunk[A] = Unit -> A;

let invoke[A] (t : Thunk[A]) : A =
    t ();

type Show[A] = A -> String;

data Maybe[A] = Nothing
              | Just A
              ;

let maybeToString[A] (tostr : Show[A]) (m : Maybe[A]) : String =
    case m of
        Nothing     -> "Nothing"
     |  Just x      -> "Just \{tostr x}";


data PList[A] = Nil
              | Cons A (Thunk[PList[A]])
              ;

let rec nats (beg : Int) : PList[Int] =
    Cons[Int] beg (\(__ : Unit) -> nats (beg+1));


let singleton[A] (x : A) : PList[A] =
    Cons[A] x (\(__: Unit) -> (Nil[A]));

let rec map[A, B] (f : A -> B) (l : PList[A]) : PList[B] =
    case l of
        Nil             ->      Nil[B]
     |  Cons x xs       ->      Cons[B] (f x) (\(__ : Unit) -> (map[A, B] f (invoke[PList[A]] xs)));

let rec foldl[A, B] (f : B -> A -> B) (z : B) (l : PList[A]) : B =
    case l of
        Nil             ->      z
     |  Cons x xs       ->      foldl[A, B] f (f z x) (invoke[PList[A]] xs);


let foldr[A, B] (f : A -> B -> B) (z : B) (l : PList[A]) : B =
    let rec inner (s : PList[A]) (k : B -> B) : B =
        case s of
            Nil         -> k z
         |  Cons x xs   -> inner (invoke[PList[A]] xs) (\(y : B) -> k (f x y));
    inner l (\(x : B) -> x);


let join[A] (l : PList[A]) (j : A) : PList[A] =
    let rec inner[A] (l : PList[A]) (j : A) : PList[A] =
        case l of
            Nil         -> (Nil[A])
         |  Cons x xs   -> (Cons[A] j (\(__ : Unit) ->
                                (Cons[A] x (\(__ : Unit) -> inner[A] (invoke[PList[A]] xs) j))));
    case l of
        Nil         -> (Nil[A])
     |  Cons x xs   -> (Cons[A] x (\(__ : Unit) -> inner[A] (invoke[PList[A]] xs) j));

let (+>)[A] (a : A) (l : PList[A]) : PList[A] =
    Cons[A] a (\(__ : Unit) -> l);

let rec concat2[A] (a : PList[A]) (b : PList[A]) : PList[A] =
    case a of
        Nil         ->  b
      | Cons x xs   ->  Cons[A] x (\(__ : Unit) -> (concat2[A] (invoke[PList[A]] xs) b));

let rec lazyconcat2[A] (a : PList[A]) (b : Thunk[PList[A]]) : PList[A] =
    case a of
        Nil         ->  (invoke[PList[A]] b)
      | Cons x xs   ->  Cons[A] x (\(__ : Unit) -> (lazyconcat2[A] (invoke[PList[A]] xs) b));

let (++)[A] (a : PList[A]) (b : PList[A]) : PList[A] =
    concat2[A] a b;

let (+~)[A] (a : PList[A]) (b : Thunk[PList[A]]) : PList[A] =
    lazyconcat2[A] a b;

let rec concat[A] (l : PList[PList[A]]) : PList[A] =
    case l of
        Nil         ->  Nil[A]
     |  Cons x xs   ->  x +~[A] (\(__ : Unit) -> (concat[A] (invoke[PList[PList[A]]] xs)));

let rec len[A] (l : PList[A]) : Int =
    case l of
        Nil         ->  0
     |  Cons _ xs   ->  1 + (len[A] (invoke[PList[A]] xs));

let isempty[A] (l : PList[A]) : Bool =
    case l of
        Nil         -> True
     |  Cons _ _    -> False;

let first[A] (l : PList[A]) : Maybe[A] =
    case l of
        Nil         -> Nothing[A]
     |  Cons x _    -> Just[A] x;

let rest[A] (l : PList[A]) : Maybe[PList[A]] =
    case l of
        Nil         -> Nothing[PList[A]]
     |  Cons _ xs   -> Just[PList[A]] (invoke[PList[A]] xs);

let rec take[A] (n : Int) (l : PList[A]) : PList[A] =
    if n <= 0 then
        (Nil[A])
    else
        case l of
            Nil         ->  Nil[A]
         |  Cons x xs   ->  Cons[A] x (\(__ : Unit) -> (take[A] (n-1) (invoke[PList[A]] xs)));

let rec zip[A, B] (a : PList[A]) (b : PList[B]) : PList[(A, B)] =
    case a of
        Nil         -> Nil[(A, B)]
     |  Cons xa xsa ->
            case b of
                Nil         -> Nil[(A, B)]
             |  Cons xb xsb ->
                    Cons[(A, B)] (xa, xb) (\(__ : Unit) -> zip[A, B] (invoke[PList[A]] xsa) (invoke[PList[B]] xsb));

let pListToString[A] (tostr : A -> String) (l : PList[A]) : String =
    type StringBuilder = java.lang.StringBuilder;

    let inner = foldl[String, StringBuilder]
                    (\(b : StringBuilder) (a : String) -> b.append(a))
                    (new java.lang.StringBuilder("["))
                    (join[String] (map[A, String] tostr l) ", ");

    inner.append("]").toString();


data Doc =	NIL
        |	TEXT String Doc
		|	LINE Int Doc
;

let nil = NIL
;

let rec (<>) (x: Doc) (y: Doc) : Doc=
	case x of
			TEXT s d 	-> TEXT s (d <> y)
		|	LINE i e	-> LINE i (e <> y)		
		|	NIL 		-> y
;

let rec nest (i: Int) (x: Doc): Doc=
	case x of
			TEXT s d 	-> TEXT s (nest i d)
		|	LINE j c 	-> LINE (i+j) (nest i c)
		|	NIL 		-> NIL
;

let text (s: String): Doc=
	TEXT s NIL
;

let line: Doc= 
	LINE 0 NIL
;

let concat (x : String) (y : String): String= 
	x.concat(y)
;

let rec space (i: Int): String=
	if i==0 then
				""
			 else
			 	" ".concat(space (i-1))
;

let rec tab (i: Int): String=
	if i==0 then
				""
			 else
			 	"\t".concat(tab (i-1))
;

let rec showDoc(doc: Doc): String=
	case doc of
			TEXT s x	-> concat s (showDoc x)
		|	LINE i d 	-> concat (concat "\n" (space i)) (showDoc d)
		| 	NIL			-> ""
;
data F2JType = F2JNormalType    String PList[F2JType]
             | F2JPairedType    PList[F2JType]
             | F2JFunctionType  F2JType F2JType
;


data F2JBindingParam = F2JBindingParam String F2JType
;


data F2JADTAlternative = F2JADTAlternative String PList[F2JType]
;


data F2JADTRecordItem = F2JADTRecordItem String F2JType
;


data F2JADTBody = F2JADTNormalBody F2JType PList[F2JADTAlternative]
                | F2JADTRecordBody F2JType PList[F2JADTRecordItem]
;

data rec
    F2JBindingBody = F2JLetBindingBody     String PList[F2JType] PList[F2JBindingParam] Maybe[F2JType] F2JExpr
                   | F2JLetRecBindingBody  String PList[F2JType] PList[F2JBindingParam] F2JType        F2JExpr
 
and F2JApplicationParam = F2JApplicationParamExpr F2JExpr
                        | F2JApplicationParamType PList[F2JType]

and F2JCaseAlternative = F2JCaseAlternative String PList[String] F2JExpr

and F2JRecordItem = F2JRecordItem String F2JExpr

and F2JExpr = F2JApplication        F2JExpr                  F2JApplicationParam
            | F2JLet                PList[F2JBindingBody]    F2JExpr
            | F2JLetRec             PList[F2JBindingBody]    F2JExpr
            | F2JLambda             PList[F2JBindingParam]   F2JExpr
            | F2JCase               F2JExpr                  PList[F2JCaseAlternative]
            | F2JADT                PList[F2JADTBody]        F2JExpr
            | F2JRecADT             PList[F2JADTBody]        F2JExpr
            | F2JTypeAlias          F2JType                  F2JType                     F2JExpr
            | F2JPair               PList[F2JExpr]
            | F2JIntLiteral         String
            | F2JStringLiteral      String
            | F2JVariable           String
            | F2JBlock              PList[F2JExpr]
            | F2JRecord             PList[F2JRecordItem]
; 

let lengF2JType (ftype: F2JType): Int=
	case ftype of
			F2JNormalType name types -> name.length()
		|	F2JPairedType types		 -> 4
		|	F2JFunctionType t1 t2    -> 4
;


let rec showF2JType (ftype: F2JType): Doc=
	case ftype of
			F2JNormalType name params -> 
			{
				if isempty[F2JType] params then 
				text name
				else 
				text name <> text "[" <> (showF2JTypes params ",") <> text "]"				
			}
		|	F2JPairedType types		  -> text "(" <> (showF2JTypes types ",") <> text ")"
		|	F2JFunctionType from to   -> showF2JType from <> text " -> " <> showF2JType to
and
showF2JTypes (ftypelist: PList[F2JType]) (decollator: String): Doc =
	case ftypelist of
			Nil						  -> NIL
		|	Cons x xs				  -> 
			{
				if isempty[F2JType] (invoke[PList[F2JType]] xs) then
				showF2JType x
				else
				showF2JType x <> (text decollator) <> (showF2JTypes (invoke[PList[F2JType]] xs) decollator)
			}
;




let rec showF2JBindingParam (fbindingparam: F2JBindingParam): Doc=
	case fbindingparam of
			F2JBindingParam name ftype ->
				text "(" <> text name <> text ": " <> showF2JType ftype <> text ")"
and showF2JBindingParams (fbindingparams: PList[F2JBindingParam]): Doc=
	case fbindingparams of
			Nil 					-> NIL
		|	Cons x xs				-> 
			{
				if isempty[F2JBindingParam] (invoke[PList[F2JBindingParam]] xs) then
				showF2JBindingParam x
				else
				showF2JBindingParam x <> text " " <> showF2JBindingParams (invoke[PList[F2JBindingParam]] xs)
			}
;



let rec showF2JADTAlternative (f2jadtalternative: F2JADTAlternative): Doc=
	case f2jadtalternative  of
			F2JADTAlternative name types	-> text "\t" <> text name <> text " " <> (showF2JTypes types " ")

and showF2JADTAlternatives (f2jadtalternatives: PList[F2JADTAlternative]) (indent: Int): Doc=
	case f2jadtalternatives of
			Nil 					-> NIL
		|	Cons x xs				->
			{
				if isempty[F2JADTAlternative] (invoke[PList[F2JADTAlternative]] xs) then
				showF2JADTAlternative x
				else
				showF2JADTAlternative x <> line <>	text (space indent) <> text "|" <> (showF2JADTAlternatives (invoke[PList[F2JADTAlternative]] xs) indent)
			}
;




let rec showF2JADTRecordItem (f2jadtrecorditem: F2JADTRecordItem): Doc=
	case f2jadtrecorditem of
			F2JADTRecordItem name ty -> text "\t" <> text name <> text "\t\t: " <> showF2JType ty

and showF2JADTRecordItems (f2jadtrecorditems: PList[F2JADTRecordItem]): Doc=
	case f2jadtrecorditems of
			Nil 					 -> NIL
		|	Cons x xs				 -> 
			{
				if isempty[F2JADTRecordItem] (invoke[PList[F2JADTRecordItem]] xs) then
				showF2JADTRecordItem x
				else
				showF2JADTRecordItem x <> text "," <> line <> showF2JADTRecordItems (invoke[PList[F2JADTRecordItem]] xs)
			}
;




let rec showF2JADTBody (f2jadtbody: F2JADTBody) (indentation: Int): Doc=
	case f2jadtbody of
			F2JADTNormalBody ftype alternatives		-> (showF2JType ftype) <> (text "= ") <> (showF2JADTAlternatives alternatives (lengF2JType(ftype) + indentation)) 
		|	F2JADTRecordBody ftype recorditems		-> (showF2JType ftype) <> (text "= {") <> line <> (showF2JADTRecordItems recorditems) <> line <> text "}" 
 
and showF2JADTBodys (f2jadtbodys: PList[F2JADTBody]) (indentation: Int): Doc=
	case f2jadtbodys of
			Nil 				-> NIL
		|	Cons x xs 			->
			{
				if isempty[F2JADTBody] (invoke[PList[F2JADTBody]] xs) then
				showF2JADTBody x indentation
				else
				showF2JADTBody x indentation <> line <> text "and" <> line <> (showF2JADTBodys (invoke[PList[F2JADTBody]] xs) indentation)
			}
;





let max (a: Int) (b: Int) : Int=
    if (a>b) then a else b
;

let rec lengOfPListString (strs: PList[String]): Int=
    case strs of
        Cons x xs -> x.length() + 1 + (lengOfPListString (invoke[PList[String]] xs))
    |   Nil       -> 0
;

let rec lengOfF2JCaseAlternative (ca: F2JCaseAlternative): Int=
    case ca of 
            F2JCaseAlternative dataname params expr -> (dataname.length()) + 1 + (lengOfPListString params)
    
and
lengOfF2JCaseAlternatives (cas: PList[F2JCaseAlternative]): Int=
    case cas of
            Nil         -> 0
        |   Cons x xs   -> max (lengOfF2JCaseAlternative x) (lengOfF2JCaseAlternatives (invoke[PList[F2JCaseAlternative]] xs))
;



let rec showF2JRecordItem (record: F2JRecordItem): Doc=
	line <> text "-- F2JRecordItem is TODO" <> line

and showF2JRecordItems (records: PList[F2JRecordItem]): Doc=
	line <> text "-- F2JRecordItems is TODO" <> line

;

let rec showPListString (ss: PList[String]): Doc=
	case ss of
			Nil 		-> NIL
		|	Cons x xs	-> text x <> text " " <> showPListString (invoke[PList[String]] xs)
;

let showMaybe[A] (show: A -> Doc) (mb: Maybe[A]): Doc=
	case mb of
			Nothing 	-> NIL
		| 	Just x 		-> show x
;


let rec showF2JBindingBody (b : F2JBindingBody): Doc =
    case b of
        	F2JLetBindingBody name args params rettype expr 	->
        	{
        		if isempty[F2JType] args then
        		text name <> text " " <> showF2JBindingParams params <> text ": " <> showMaybe[F2JType] showF2JType rettype <> text " =" <> nest 4 (line <> (showF2JExpr expr))
        		else
        		text name <> text "[" <> showF2JTypes args "," <> text "] " <> showF2JBindingParams params <> text ": " <> showMaybe[F2JType] showF2JType rettype <> text " =" <> nest 4 (line <> (showF2JExpr expr))
			}

     	|  	F2JLetRecBindingBody name args params rettype expr 	->
     		{
     			if isempty[F2JType] args then
     			text name <> text " " <> showF2JBindingParams params <> text ": " <> showF2JType rettype <> text " =" <> nest 4 (line <> (showF2JExpr expr))
     			else
     			text name <> text "[" <> showF2JTypes args "," <> text "] " <> showF2JBindingParams params <> text ": " <> showF2JType rettype <> text " =" <> nest 4 (line <> (showF2JExpr expr))
     		}
     		

and showF2JBindingBodys (bs: PList[F2JBindingBody]): Doc=
	case bs of
			Nil 						-> NIL
		|	Cons x xs					->
			{
				if isempty[F2JBindingBody] (invoke[PList[F2JBindingBody]] xs) then
				showF2JBindingBody x
				else
				showF2JBindingBody x <> line <> text "and" <> line <> showF2JBindingBodys (invoke[PList[F2JBindingBody]] xs)
			}


and showF2JApplicationParam (ap: F2JApplicationParam): Doc=
	case ap of
			F2JApplicationParamExpr expr 	->
				showF2JExpr expr
		|	F2JApplicationParamType types	->
				text "[" <> showF2JTypes types "," <>  text "]"

and showF2JCaseAlternative (ca: F2JCaseAlternative) (myLen:Int) (maxLen: Int): Doc=
	case ca of
			F2JCaseAlternative dataname params expr 		->
				text "\t" <> text dataname <> text " " <> showPListString params <> text (space (maxLen - myLen + 2)) <> text "->" <> (nest 12 (line <> showF2JExpr expr)) 

and showF2JCaseAlternatives (cas: PList[F2JCaseAlternative]) (maxLen: Int): Doc=
	case cas of
			Nil 						-> NIL
		|	Cons x xs					->
			{
				if isempty[F2JCaseAlternative] (invoke[PList[F2JCaseAlternative]] xs) then
				showF2JCaseAlternative x (lengOfF2JCaseAlternative x) maxLen
				else
				(showF2JCaseAlternative x (lengOfF2JCaseAlternative x) maxLen) <> line <> text "|" <> (showF2JCaseAlternatives (invoke[PList[F2JCaseAlternative]] xs) maxLen)
			}
and showF2JExpr (expr: F2JExpr): Doc=
	case expr of
			F2JApplication f2jexpr apparm 			-> text "(" <> showF2JExpr f2jexpr <> text " " <> showF2JApplicationParam apparm <> text ")"
		|	F2JLet bindingbodys f2jexpr 			-> text "let " <> showF2JBindingBodys bindingbodys <> line <> text ";" <> line <> showF2JExpr f2jexpr
		|	F2JLetRec bindingbodys f2jexpr 			-> text "let rec " <> showF2JBindingBodys bindingbodys <> line <> text ";" <> line <> showF2JExpr f2jexpr
		|	F2JLambda bindingparams f2jexpr 		-> text "\\" <> showF2JBindingParams bindingparams <> text " -> " <> showF2JExpr f2jexpr
		| 	F2JCase f2jexpr casealternatives  		-> text "case " <> showF2JExpr f2jexpr <> text " of" <> line <> (showF2JCaseAlternatives casealternatives (lengOfF2JCaseAlternatives casealternatives))
		|	F2JADT f2jadtbodys f2jexpr 				-> text "data " <> (showF2JADTBodys f2jadtbodys 7) <> line <> text ";" <> line <> showF2JExpr f2jexpr
		| 	F2JRecADT f2jadtbodys f2jexpr 			-> text "data rec " <> (showF2JADTBodys f2jadtbodys 11) <> line <> text ";" <> line <> showF2JExpr f2jexpr
		|	F2JTypeAlias type1 type2 f2jexpr 		-> text "type " <> showF2JType type1 <> text "= " <> showF2JType type2 <> showF2JExpr f2jexpr
		|	F2JPair exprs 							-> showF2JExprs exprs
		|	F2JIntLiteral s 						-> text s
		|	F2JStringLiteral s 						-> text s
		|	F2JVariable v 							-> text v
		|	F2JBlock exprs							-> text "{" <> line <> showF2JExprs exprs <> line <> text "}"
		|	F2JRecord recorditems 					-> showF2JRecordItems recorditems

and showF2JExprs (exprs: PList[F2JExpr]): Doc=
	case exprs of
			Nil 				-> NIL
		|	Cons x xs			-> 
			{
				if isempty[F2JExpr] (invoke[PList[F2JExpr]] xs) then
				showF2JExpr x
				else
				showF2JExpr x <> text " " <> showF2JExprs (invoke[PList[F2JExpr]] xs)
			}
;






let println (s : String) : Unit = { java.lang.System.out.println(s); () }
;
let print (s : String) : Unit = { java.lang.System.out.print(s); () }
;
type TestFn = {
    name : String,
    fn   : Unit -> Unit
}
;
let fool (x: Unit) (y: Unit): Unit= {
	()
}
;
let rec runTest (test : TestFn) : Unit = {
    let testingPrefix : String = "\u001b[31;1mtesting\u001b[0m\t";
    println testingPrefix.concat(test.name).concat(" :");
    test.fn ();
    println "...check it yourself"
}
and 
runTests (tests: PList[TestFn]): Unit= {
	case tests of 
			Nil 			-> ()
		|	Cons x xs		-> fool (runTest x) (runTests (invoke[PList[TestFn]] xs)) 
}
;


let f2jnormaltype = F2JNormalType "F2JType" (Nil[F2JType])
;
let f2jtype = F2JFunctionType (
								F2JPairedType (
												(F2JNormalType "A" (Nil[F2JType]))  +> (
																		Cons[F2JType] (F2JNormalType "B" (Nil[F2JType])) (\(__ : Unit) -> Nil[F2JType])
																		)
											  )
							  ) 
			  				  (
			  				  	F2JNormalType "C" (Nil[F2JType])
			  				  )	  

;

let f2jtypes =  (F2JNormalType "A" (Nil[F2JType])) +> 
				(
					Cons[F2JType] 
					(F2JNormalType "B" (Nil[F2JType])) 
					(\(__ : Unit) -> Nil[F2JType])
				)
;
let testF2JType : TestFn = {
    name = "F2JType",
    fn   = \(x : Unit) -> {
        println (showDoc (showF2JType f2jtype) )
    }
}
;

let fbingdingparam = F2JBindingParam "a" f2jtype
;
let fbingdingparam1 = F2JBindingParam "b" f2jtype
;
let fbingdingparam2 = F2JBindingParam "c" f2jtype
;
let fbingdingparams = fbingdingparam +> (fbingdingparam1 +> (Cons[F2JBindingParam] (fbingdingparam2) (\(__ : Unit) -> Nil[F2JBindingParam])))
;
let testF2JBindingParam : TestFn = {
    name = "F2JBindingParam",
    fn   = \(x : Unit) -> {
        println (showDoc (showF2JBindingParams fbingdingparams) )
    }
}
;

let alternative1 = F2JADTAlternative 
					"F2JNormalType" 
					(
						(F2JNormalType "String" (Nil[F2JType])) +>
						(
							Cons[F2JType]
							(F2JNormalType "PList" (Cons[F2JType] (F2JNormalType "F2JType" (Nil[F2JType])) (\(__ : Unit) -> Nil[F2JType])))
							(\(__ : Unit) -> Nil[F2JType])  
						)
					)
;
let alternative2 = F2JADTAlternative 
					"F2JPairedType" 
					(
						Cons[F2JType] 
						(F2JNormalType "PList" (Cons[F2JType] (F2JNormalType "F2JType" (Nil[F2JType])) (\(__ : Unit) -> Nil[F2JType])))
						(\(__ : Unit) -> Nil[F2JType])  
					)
;
let alternative3 = F2JADTAlternative 
					"F2JFunctionType" 
					(
						(F2JNormalType "F2JType" (Nil[F2JType]) ) +>
						(
							Cons[F2JType] 
							(F2JNormalType "F2JType" (Nil[F2JType]) )
							(
								\(__ : Unit) -> Nil[F2JType]
							)
						)  
					)
;
let adtbody1 = F2JADTNormalBody 
				(F2JNormalType "F2JType" (Nil[F2JType])) 
				(
						(alternative1) +>
						(
							(alternative2) +>
							(
								Cons[F2JADTAlternative]
								(alternative3)
								(\(__ : Unit) -> Nil[F2JADTAlternative])
							)
						)  
				)
;
let testF2JADTBody1 : TestFn = {
    name = "F2JADTBody  normal",
    fn   = \(x : Unit) -> {
        println (showDoc (showF2JADTBody adtbody1 7) )
    }
}
;

let f2jlambda= F2JLambda fbingdingparams (F2JStringLiteral "1")
;
let testF2JLambda : TestFn = {
    name = "F2JLambda",
    fn   = \(x : Unit) -> {
        println (showDoc (showF2JExpr f2jlambda) )
    }
}
;

let recorditem1= F2JADTRecordItem "name" (F2JNormalType "Demonsu" (Nil[F2JType]))
;
let recorditem2= F2JADTRecordItem "age" (F2JNormalType "24" (Nil[F2JType]))
;
let recorditem3= F2JADTRecordItem "gender" (F2JNormalType "male" (Nil[F2JType]))
;
let adtbody2 = F2JADTRecordBody
				(F2JNormalType "F2JRecord" (Nil[F2JType]))
				(
						(recorditem1) +>
						(
							(recorditem2) +>
							(
								Cons[F2JADTRecordItem]
								(recorditem3)
								(\(__ : Unit) -> Nil[F2JADTRecordItem])
							)
						)  
				)	
;	
let testF2JADTBody2 : TestFn = {
    name = "F2JADTBody  record",
    fn   = \(x : Unit) -> {
        println (showDoc (showF2JADTBody adtbody2 7) )
    }
}
;

let maybe= Just[F2JType] f2jtype
;

let testMaybe : TestFn = {
    name = "Maybe",
    fn   = \(x : Unit) -> {
        println (showDoc (showMaybe[F2JType] showF2JType maybe) )
    }
}
;

let maybenormal= Just[F2JType] f2jnormaltype
;
let f2jbingdingbody = F2JLetBindingBody "PrintWorld" f2jtypes fbingdingparams maybenormal (F2JVariable "test")
;
let testF2JLetBindingBody : TestFn = {
    name = "F2JLetBindingBody",
    fn   = \(x : Unit) -> {
        println (showDoc (showF2JBindingBody f2jbingdingbody) )
    }
}
;
let f2jbingdingbodyrec = F2JLetRecBindingBody "PrintWorld" f2jtypes fbingdingparams f2jnormaltype (F2JVariable "test")
;
let testF2JLetBindingBodyRec : TestFn = {
    name = "F2JLetBindingBody rec",
    fn   = \(x : Unit) -> {
        println (showDoc (showF2JBindingBody f2jbingdingbodyrec) )
    }
}
;
let f2jexpr_f2jadt= F2JADT (adtbody1 +> (Cons[F2JADTBody] adtbody2 (\(__ : Unit) -> Nil[F2JADTBody]))) (F2JVariable "test")
;
let testF2JExprF2JADT : TestFn = {
    name = "F2JExpr(F2JADT)",
    fn   = \(x : Unit) -> {
        println (showDoc (showF2JExpr f2jexpr_f2jadt) )
    }
}
;
let f2jexpr_f2jrecadt= F2JRecADT (adtbody1 +> (Cons[F2JADTBody] adtbody1 (\(__ : Unit) -> Nil[F2JADTBody]))) (F2JVariable "test")
;
let testF2JExprF2JRecADT : TestFn = {
    name = "F2JExpr(F2JRecADT)",
    fn   = \(x : Unit) -> {
        println (showDoc (showF2JExpr f2jexpr_f2jrecadt) )
    }
}
;

let f2jexpr_app = F2JApplication (F2JVariable "text") (F2JApplicationParamExpr (F2JVariable "s"))
;
let f2jexpr_app1 = F2JApplication (F2JVariable "<>") (
					F2JApplicationParamExpr
						(F2JPair
						(  
							(F2JApplication (F2JVariable "showF2JType") (F2JApplicationParamExpr (F2JVariable "type1"))) +>
								(Cons[F2JExpr]
									(F2JApplication (F2JVariable "showF2JType") (F2JApplicationParamExpr (F2JVariable "type2")))
									(\(__ : Unit) -> Nil[F2JExpr])
								)
						)
						)
	)
;
let f2jexpr_app2 = F2JApplication (F2JVariable "showF2JTypes") (F2JApplicationParamExpr (F2JVariable "types"))
;

let casealternative1= F2JCaseAlternative "F2JNormalType" ("s" +> (Cons[String] "types" (\(__ : Unit) -> Nil[String]))) (f2jexpr_app)
;
let casealternative2= F2JCaseAlternative "F2JFunctionType" ("type1" +> (Cons[String] "type2" (\(__ : Unit) -> Nil[String]))) (f2jexpr_app1)
;
let casealternative3= F2JCaseAlternative "F2JPairedType" (Cons[String] "types" (\(__ : Unit) -> Nil[String])) (f2jexpr_app2)
;
let casealternatives= casealternative1 +> (casealternative2 +> (Cons[F2JCaseAlternative] casealternative3 (\(__ : Unit) -> Nil[F2JCaseAlternative]) ))
;
let f2jexpr_f2jcase= F2JCase (F2JVariable "x") casealternatives
;
let testF2JExprF2JCase : TestFn = {
    name = "F2JExpr(F2JCase)",
    fn   = \(x : Unit) -> {
        println (showDoc (showF2JExpr f2jexpr_f2jcase) )
    }
}
;
let f2jnormaltype1 = F2JNormalType "F2JType" (Nil[F2JType])
;
let f2jnormaltype2 = F2JNormalType "Doc" (Nil[F2JType])
;
let maybenormal1= Just[F2JType] f2jnormaltype2
;
let f2jbindingbody2= F2JLetBindingBody "showF2JType" (Nil[F2JType]) (Cons[F2JBindingParam] (F2JBindingParam "x" f2jnormaltype1) (\(__ : Unit) -> Nil[F2JBindingParam]) ) maybenormal1  f2jexpr_f2jcase
;
let f2jexpr_f2jfull= F2JADT (Cons[F2JADTBody] adtbody1 (\(__ : Unit) -> Nil[F2JADTBody])) 
					(F2JLet (Cons[F2JBindingBody] f2jbindingbody2 (\(__ : Unit) -> Nil[F2JBindingBody])) (F2JVariable "end"))
;
let testF2JExprFull : TestFn = {
    name = "F2JExpr(Full)",
    fn   = \(x : Unit) -> {
        println (showDoc (showF2JExpr f2jexpr_f2jfull) )
    }
}
;

let tests=	(testF2JExprFull			+>[TestFn]
			(testF2JExprF2JCase 		+>[TestFn]
			(testF2JExprF2JRecADT		+>[TestFn]	
			(testF2JExprF2JADT 			+>[TestFn]
			(testF2JLetBindingBodyRec 	+>[TestFn]
			(testF2JLetBindingBody 		+>[TestFn]
			(testMaybe 					+>[TestFn]
			(testF2JADTBody2 			+>[TestFn]
		 	(testF2JADTBody1 			+>[TestFn]
			(testF2JBindingParam 		+>[TestFn] 
			(testF2JLambda				+>[TestFn]
			(testF2JType 				+>[TestFn] 
			(Nil[TestFn])))))))))))))
;
runTests tests

