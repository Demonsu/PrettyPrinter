-- complete pretty printer library

data Doc =	NIL
        |	TEXT String
		|	LINE
		|	NEST Int Doc
		|	UNION Doc Doc
		|   CONCAT Doc Doc
;

-- for docpair list, seperating from lazy PList outside

data DList[A] = DNil
              | DCons A (DList[A])
;


-- print doc, for converting into string

data PDoc = NI
		|	TE String PDoc
		|	LI Int PDoc
;

type Pair = (Int, Doc)
;

let nil = NIL
;

-- union operation
let (<|>) (x: Doc) (y: Doc) : Doc =
	UNION x y
;


let text (s: String): Doc =
	TEXT s
;

let line: Doc = 
	LINE
;

-- union laws
let (<>) (x: Doc) (y: Doc) : Doc =
	CONCAT x y
;

let nest (i: Int) (x: Doc): Doc =
	NEST i x
;

-- kill all the line, replace with sapce

let rec flatten (d: Doc): Doc =
	case d of
			NIL 			-> NIL
		|	LINE 			-> TEXT " "
		|	TEXT s 			-> TEXT s 
		|	NEST i x 		-> NEST i (flatten x)
		|	UNION x y		-> flatten x
		|	CONCAT x y 		-> (flatten x) <> (flatten y)
;

-- new api for user, tell lib where you want minimum line nums

let rec group (d: Doc): Doc =
	(flatten d) <|> d
;

-- judge whther the doc fit the line width

let rec fits (w: Int) (d: PDoc): Bool=
	if w < 0 then False
	else 
		case d of
				NI 				-> True
			|	TE s x  		-> fits (w - s.length()) x
			|	LI i x 	  		-> True
;

-- return a better one, here better means max width within limit 

let better (w: Int) (k: Int) (x: PDoc) (y: PDoc): PDoc =
	if fits (w - k) x then x
	else  y
;



let rec be (w: Int) (k: Int) (docs: DList[Pair]): PDoc =
	case docs of
			DNil 					-> NI
		|	DCons x xs				->
			{
				case x._2 of
					NIL 			-> be w k xs
				|	CONCAT d1 d2	-> be w k (DCons[Pair] (x._1, d1) (DCons[Pair] (x._1, d2) xs))                       
				|	NEST j d		-> be w k (DCons[Pair] (j + x._1, d)  xs)
				|   TEXT s 			-> TE s (be w k xs)
				| 	LINE 			-> LI x._1 (be w k xs)
				|	UNION d1 d2 	-> better w k (be w k (DCons[Pair] (x._1, d1) xs)) (be w k (DCons[Pair] (x._1, d2) xs))

			}


;

-- return the best layout among groups

let best (w: Int) (k: Int) (d: Doc): PDoc =

	be w k (DCons[Pair] (0, d) (DNil[Pair]))
;

let concat (x : String) (y : String): String= 
	x.concat(y)
;

let rec space (i: Int): String=
	if i==0 then
				""
			 else
			 	" ".concat(space (i-1))
;

let rec showDoc(doc: PDoc): String=
	case doc of
			TE s x	-> concat s (showDoc x)
		|	LI i d 	-> concat (concat "\n" (space i)) (showDoc d)
		| 	NI			-> ""
;

let pretty (w: Int) (d: Doc): String =
	showDoc (best w 0 d)
;



